plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

android {
    compileSdk 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "io.explod.paging"
        minSdk 23
        targetSdk 30
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "io.explod.paging.testing.AndroidTestInstrumentationRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildFeatures {
        dataBinding true
    }
    buildTypes {
        debug {
            buildConfigField "String", "APP_USER_AGENT", "\"Paging-debug ${defaultConfig.versionName} ${defaultConfig.versionCode}\""
            buildConfigField "String", "GITHUB_CLIENT_TOKEN", "\"\"" // Add your github personal access token here for higher API quota
        }
        release {
            buildConfigField "String", "APP_USER_AGENT", "\"Paging ${defaultConfig.versionName} ${defaultConfig.versionCode}\""
            buildConfigField "String", "GITHUB_CLIENT_TOKEN", "\"\"" // Add your github personal access token here for higher API quota

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    sourceSets {
        main.java.srcDirs += ["src/main/kotlin"]

        String sharedTestDir = "src/sharedTest/kotlin"
        test {
            debug.assets.srcDirs += files("$projectDir/schemas".toString())
            debug.assets.srcDirs += files("$projectDir/assets".toString())
            debug.assets.srcDirs += files("$projectDir/raw".toString())

            java.srcDirs += ["src/test/kotlin"]
            java.srcDirs += sharedTestDir
            kotlin.srcDirs += sharedTestDir
        }
        androidTest {
            assets.srcDirs += files("$projectDir/schemas".toString())

            java.srcDirs += ["src/androidTest/kotlin"]
            java.srcDirs += sharedTestDir
            kotlin.srcDirs += sharedTestDir
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        unitTests.all {
            include '**/*Tests.class'
            include '**/*Test.class'

            ignoreFailures false

            systemProperty 'robolectric.logging.enabled', 'true'
            systemProperty 'robolectric.looperMode', 'PAUSED'

            jvmArgs '-noverify'
            maxHeapSize = '2g'
        }
    }
    packagingOptions {
        exclude 'META-INF/*'
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs += [
                "-Xopt-in=kotlin.contracts.ExperimentalContracts",
                "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
                "-Xopt-in=androidx.paging.ExperimentalPagingApi",
        ]
    }
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_version"

    // Android + Androidx
    implementation "androidx.core:core-ktx:1.5.0"
    implementation "androidx.appcompat:appcompat:1.3.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation "androidx.fragment:fragment-ktx:1.3.4"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.3.1"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "androidx.paging:paging-runtime-ktx:3.0.0"
    implementation "androidx.room:room-ktx:2.3.0"
    implementation "androidx.room:room-runtime:2.3.0"
    kapt "androidx.room:room-compiler:2.3.0"
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.1.5"
    implementation 'com.google.android.material:material:1.3.0'

    // Dagger
    implementation "com.google.dagger:dagger:2.35.1"
    kapt "com.google.dagger:dagger-compiler:2.35.1"
    kaptTest "com.google.dagger:dagger-compiler:2.35.1"
    kaptAndroidTest "com.google.dagger:dagger-compiler:2.35.1"

    // Glide
    implementation "com.github.bumptech.glide:glide:4.12.0"
    kapt "com.github.bumptech.glide:compiler:4.12.0"

    // JSR-310 Dates and Times
    implementation "com.jakewharton.threetenabp:threetenabp:1.3.1"

    // OkHttp
    implementation "com.squareup.okhttp3:okhttp:5.0.0-alpha.2"
    implementation "com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.2"

    // Retrofit
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.squareup.retrofit2:converter-scalars:2.9.0"
    implementation "com.squareup.retrofit2:retrofit:2.9.0"

    // Timber
    implementation "com.jakewharton.timber:timber:4.7.1"

    // Tests
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "androidx.paging:paging-common-ktx:3.0.0"
    testImplementation "androidx.room:room-testing:2.3.0"
    testImplementation "androidx.test:core-ktx:1.3.0"
    testImplementation "androidx.test.espresso:espresso-core:3.3.0"
    testImplementation "androidx.test.espresso:espresso-intents:3.3.0"
    testImplementation "androidx.test.ext:junit:1.1.2"
    testImplementation "androidx.test.ext:truth:1.3.0"
    testImplementation "com.android.support.test.espresso:espresso-contrib:3.0.2"
    testImplementation "com.squareup.okhttp3:mockwebserver:4.9.1"
    testImplementation "io.mockk:mockk:1.11.0"
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.0-RC-native-mt"
    testImplementation "org.mockito:mockito-core:3.9.0"
    testImplementation("org.robolectric:robolectric:4.5.1") {
        exclude group: "com.google.protobuf", module: "protobuf-java"
    }

    // Android Tests
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation "androidx.room:room-testing:2.3.0"
    androidTestImplementation "androidx.test:core-ktx:1.3.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"
    androidTestImplementation "androidx.test.espresso:espresso-intents:3.3.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.test.ext:truth:1.3.0"
    androidTestImplementation "androidx.test:core:1.3.0"
    androidTestImplementation "androidx.test:rules:1.3.0"
    androidTestImplementation "androidx.test:runner:1.3.0"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:3.0.2"
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:4.9.1"
    androidTestImplementation "io.mockk:mockk-android:1.11.0"
    androidTestImplementation "org.mockito:mockito-android:3.9.0"
    androidTestImplementation "org.robolectric:annotations:4.5.1"
}

